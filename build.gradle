plugins {
    id 'application'

    id "com.asarkar.gradle.build-time-tracker" version "5.0.1"
    id "com.github.ben-manes.versions" version "0.53.0"
    id "org.owasp.dependencycheck" version "12.2.0"
}

group 'uk.co.kleindelao.tools'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation      libs.commons.lang3
    implementation      libs.openpdf

    testImplementation  libs.bundles.junit.jupiter
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

application {
    mainClass = 'uk.co.kleindelao.mathworksheets.output.WorksheetGenerator'
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { otherIt -> version.toUpperCase().contains(otherIt) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}
